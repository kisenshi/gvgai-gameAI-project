BasicGame key_handler=Pulse no_players=2 square_size=50
    SpriteSet
        floor > Immovable img=aimodule/chamber_floor
        trap > Immovable img=aimodule/it_is_a_trap

        wall > Immovable img=aimodule/chamber_wall

        cake > Immovable img=aimodule/cake

        laser > Missile img=aimodule/shot_laser

        portalShot > Missile singleton=True
            portalShotBlue > img=aimodule/shot_portal_blue
            portalShotOrange > img=aimodule/shot_portal_orange

        portal > Immovable
            disabledPortal >
                portalBlueDisabled > img=aimodule/chamber_wall_blue
                portalOrangeDisabled > img=aimodule/chamber_wall_orange

            workingPortal > Portal
                portalBlue > stype=portalOrange img=aimodule/chamber_wall_blue
                portalOrange > stype=portalBlue img=aimodule/chamber_wall_orange

        movable >
            subjectAvatar >
                noShooterSubject > MovingAvatar
                    noShooterBlue > img=aimodule/chell_blue
                    noShooterOrange > img=aimodule/chell_orange
                subject > ShootAvatar
                    subjectBlue > stype=portalShotBlue img=aimodule/chell_blue
                    subjectOrange > stype=portalShotOrange img=aimodule/chell_orange

            turret > ShootAvatar stype=laser img=aimodule/turret

    LevelMapping
        . > floor
        w > wall
        x > trap
        A > subjectBlue floor
        T > turret floor
        c > cake floor

    InteractionSet
        movable wall movable EOS > stepBack
        laser wall > killSprite
        turret cake portal > stepBack
        subjectAvatar laser trap > killSprite
        cake subjectAvatar > killSprite

        # To avoid infinite teleportation the exit portal is disabled during a small period of time
        # NOTE: The logic has been updated to disable the avatar instead as there was a small bug
        # Leaving the old code for future reference
        #subject portalBlue > transformToAll stype=portalOrange stypeTo=portalOrangeDisabled
        #subject portalBlue > addTimer timer=35 ftype=transformToAll stype=portalOrangeDisabled stypeTo=portalOrange
        #subject portalOrange > transformToAll stype=portalBlue stypeTo=portalBlueDisabled
        #subject portalOrange > addTimer timer=35 ftype=transformToAll stype=portalBlueDisabled stypeTo=portalBlue

        # If the portal is enabled, Chell is teleported to the other one
        subject workingPortal > teleportToExit

        # Disabling shooting avatar after teleport for a brief period of time
        # This disables infinite loop teleport AND fixes small bug of multiple portal creation
        subjectBlue workingPortal > addTimer timer=20 ftype=transformToAll stype=noShooterBlue stypeTo=subjectBlue
        subjectBlue workingPortal > transformTo stype=noShooterBlue
        subjectOrange workingPortal > addTimer timer=20 ftype=transformToAll stype=noShooterOrange stypeTo=subjectOrange
        subjectOrange workingPortal > transformTo stype=noShooterOrange

        # Logic to create Portals when shooting to a wall. Still can't believe it is working *.*
        portalShotBlue wall  > transformToAll stype=subjectBlue stypeTo=subjectOrange
        portalShotBlue wall  > killSprite
        wall portalShotBlue > transformToAll stype=portalBlue stypeTo=wall
        wall portalShotBlue > killAll stype=subjectBlue
        wall portalShotBlue > transformTo stype=portalBlue

        portalShotOrange wall  > transformToAll stype=subjectOrange stypeTo=subjectBlue
        portalShotOrange wall  > killSprite
        wall portalShotOrange > transformToAll stype=portalOrange stypeTo=wall
        wall portalShotOrange > killAll stype=subjectOrange
        wall portalShotOrange > transformTo stype=portalOrange

        # shots should not go through Portals
        portalShotBlue portal > killSprite
        portalShotOrange portal > killSprite

    TerminationSet
        SpriteCounter stype=cake    limit=0 win=True,False
        MultiSpriteCounter stype1=subject stype2=noShooterSubject limit=0 win=False,True


